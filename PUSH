//// S.R.N Robotics
// Nadia Al-Shewear
// Rogelio Lucio
// Samuel Winburn
#include <DFRobot_BMI160.h>
#define ULTRA_SAMPLES 5  // Number of samples for moving average
float filteredDistance = 0;

float ultraDistanceBuffer[ULTRA_SAMPLES] = {0};  // Buffer for ultrasonic readings
int ultraIndex = 0;  // Current index in the buffer

SemaphoreHandle_t stateMutex;

const int R_LED = 12;
const int G_LED = 14;
const int B_LED = 27;

// IR Sensor Pins 
int IRSensor1 = 36;
int IRSensor2 = 39;
int IRSensor3 = 34;
int IRSensor4 = 35;

const int trigPin = 26; 
const int echoPin = 25; 

// Declarations for the BMI160
DFRobot_BMI160 bmi160;
const int8_t i2c_addr = 0x69;


// Motor Control Pins
const int L_IN1 = 18;   // Control pin 1 for left motor
const int L_IN2 = 19;   // Control pin 2 for left motor
const int R_IN3 = 4;   // Control pin 1 for right motor
const int R_IN4 = 16;  // Control pin 2 for right motor

// PWM frequency and resolution settings for ESP32
const int PWMFreq = 1000;       // PWM frequency in Hz
const int PWMResolution = 8;    // PWM resolution (8 bits)
const int MAX_SPEED = 255;      // Maximum PWM duty cycle


// Thresholds
const int detectionThreshold = 45;

// State Machine
enum State { SEARCHING, MOVING_FORWARD, AVOID, STOPPED };
volatile State currentState = SEARCHING;

volatile uint8_t boundaryCode = 0xF;
volatile bool opponentDetected = false;

// Task handles
TaskHandle_t Task1;  // Sensor reading task
TaskHandle_t Task2;  // Motor control task

void IR_Sensor_setup();
uint8_t IR_Sensor_read();
void ultra_Sensor_setup();
float ultra_Sensor_read();
void TaskSensors(void *pvParameters);
void TaskNavigation(void *pvParameters);
void StopMotors();
void Forward();
void changeState();
void RotateSearch();


void setup() {
  Serial.begin(115200);

  IR_Sensor_setup();
  ultra_Sensor_setup();

  stateMutex = xSemaphoreCreateMutex();

  pinMode(R_LED, OUTPUT);
  pinMode(G_LED, OUTPUT);
  pinMode(B_LED, OUTPUT);

  // Motor pin setup
  ledcAttach(L_IN1,  PWMFreq, PWMResolution);
  ledcAttach(L_IN2,  PWMFreq, PWMResolution);
  ledcAttach(R_IN3,  PWMFreq, PWMResolution);
  ledcAttach(R_IN4,  PWMFreq, PWMResolution);

  //init the hardware bmin160  
  if (bmi160.softReset() != BMI160_OK){
    Serial.println("reset false");
    while(1);
  }
  
  //set and init the bmi160 i2c address
  if (bmi160.I2cInit(i2c_addr) != BMI160_OK){
    Serial.println("init false");
    while(1);
  }

  // Create the sensor and navigation tasks
  xTaskCreatePinnedToCore(TaskSensors, "TaskSensors", 10000, NULL, 2, &Task1, 1);  // Core 1 for sensors and state management
  xTaskCreatePinnedToCore(TaskNavigation, "TaskNavigation", 10000, NULL, 3, &Task2, 0);  // Core 0 for motor control
}

void loop() {
}

// Core 1: Sensor data collection and state management
// In TaskSensors:
void TaskSensors(void *pvParameters) {
  for (;;) {
    uint8_t newBoundaryCode = IR_Sensor_read();
    float distance = ultra_Sensor_read();

    xSemaphoreTake(stateMutex, portMAX_DELAY);
    boundaryCode = newBoundaryCode;

    // Determine if opponent is detected based on distance
    if (distance > 0 && distance <= detectionThreshold) {
      opponentDetected = true;  // Opponent detected
    } else {
      opponentDetected = false; // No opponent detected
    }
    xSemaphoreGive(stateMutex);

    changeState();

    vTaskDelay(5 / portTICK_PERIOD_MS);
  }
}




// Core 0: Navigation and motor control
void TaskNavigation(void *pvParameters) {
  for (;;) {

    switch (currentState) {
      case SEARCHING:
      digitalWrite(R_LED, LOW);
      digitalWrite(G_LED, HIGH);
      digitalWrite(B_LED, LOW);
        StopMotors();
        RotateSearch();
        break;
      case MOVING_FORWARD:
      digitalWrite(R_LED, HIGH);
      digitalWrite(G_LED, LOW);
      digitalWrite(B_LED, LOW);
        Forward();
        break;
      case AVOID:
      digitalWrite(R_LED, LOW);
      digitalWrite(G_LED, LOW);
      digitalWrite(B_LED, HIGH);
        handleBoundaryMovement();
        break;
      case STOPPED:
      digitalWrite(R_LED, LOW);
        StopMotors();
        break;
    }

    vTaskDelay(200 / portTICK_PERIOD_MS);
  }
}

// Handle state changes based on sensor input
void changeState() {
  static State lastState = STOPPED;

  if (boundaryCode != 0xF) {
    currentState = AVOID;
  } else if (opponentDetected) {
    currentState = MOVING_FORWARD;
  } else {
    currentState = SEARCHING;
  }
}

void handleBoundaryMovement() {
  switch (boundaryCode) {
    case 0x7:
      Forward();
      Serial.println("7.");
      break;
    case 0xB:
      Forward();
      Serial.println("B");
      break;
    case 0xD:
      StraightBack();
      RotateToBigAngle(120);
      Serial.println("D-front-right");
      break;
      case 0xE:
      StraightBack();
      RotateToBigAngle(-120);
      Serial.println("E-front-left");
      break;
    case 0x3:
      Forward();
      Serial.println("3");
      break;
    case 0xC:
      StraightBack();
      RotateToBigAngle(180);
      Serial.println("C-2front");
      break;
    case 0x5:
      RotateToBigAngle(180);
      Serial.println("5");
      break;
    case 0xA:
     RotateToBigAngle(180);
      Serial.println("A");
      break;
    default:
     StopMotors();
      Serial.println("Rotating DEFAULT.");
      break;
  }
}

void IR_Sensor_setup() {
  pinMode(IRSensor1, INPUT);
  pinMode(IRSensor2, INPUT);
  pinMode(IRSensor3, INPUT);
  pinMode(IRSensor4, INPUT);
}

uint8_t IR_Sensor_read() {
  int status1 = digitalRead(IRSensor1);
  int status2 = digitalRead(IRSensor2);
  int status3 = digitalRead(IRSensor3);
  int status4 = digitalRead(IRSensor4);

  uint8_t boundaryCode = (status4 << 3) | (status3 << 2) | (status2 << 1) | status1;
  
  Serial.print("Sensor States: ");
  Serial.print("Front-Left: "); Serial.print(status1);
  Serial.print(" | Front-Right: "); Serial.print(status2);
  Serial.print(" | Back-Left: "); Serial.print(status3);
  Serial.print(" | Back-Right: "); Serial.println(status4);
  Serial.print("Boundary Code: ");
  Serial.println(boundaryCode, BIN);

  return boundaryCode;
}

void ultra_Sensor_setup() {
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
}

// Enhanced Ultrasonic Sensor Read
float ultra_Sensor_read() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  long duration = pulseIn(echoPin, HIGH, 30000); // Timeout at 30ms
  if (duration == 0) {
    return -1;  // No signal detected
  }
  
  float distance = (duration * 0.034) / 2;  // Convert to cm
  filteredDistance = updateUltraAverage(distance);
  return filteredDistance;
}

// Read the Gyroscope
float readGyro(){
  float angle = 0;
  int rslt;
  int trash;
  float Yaw = 0;
  ////only read gyro data from bmi160
  int16_t accelGyro[6]={0}; 
  //get both accel and gyro data from bmi160
  //parameter accelGyro is the pointer to store the data
  rslt = bmi160.getAccelGyroData(accelGyro);

  if(rslt == 0){
    for(int i=0;i<6;i++){
      if (i==2){
        //the first three are gyro data
        Yaw = (((accelGyro[i]+11.85)/128)*0.6);
        if(Yaw > 0.15){
          angle += Yaw;
        }
        else if(Yaw <= 0.15 and Yaw >=-0.15){
          angle += 0;
        }
        else{
          angle -= abs(Yaw);
        }
        //Serial.print(Yaw);Serial.print("\t"); Serial.print(Yaw/3.14); Serial.print("\t"); Serial.print(angle); Serial.print("\t");
        //Serial.println(accelGyro[i]*3.14/180.0);
      }
      else{
        //the following three data are accel data
        trash = accelGyro[i];
      }
    }
    
    //Serial.println();
  }else{
    Serial.println("err");
  }
  delay(50);
  return angle;
}

// Rotate to any desired angle less than 90 degrees
void RotateToSmallAngle(float setpoint){
  float angle = 0;
  float error = setpoint;
  float PWM_L = 0;
  float PWM_R = 0;
  float Kp = 180/setpoint;
  if (error < 0){
    while (error <= -2){
      PWM_L = error * Kp;
      PWM_R = error * Kp;

      ledcWrite(L_IN1, int(PWM_L)); // L Forward
      ledcWrite(L_IN2, 0);          // L Backward == 0
      ledcWrite(R_IN3, 0);          // R Forward == 0
      ledcWrite(R_IN4, int(PWM_R)); // R Backward
      
      angle += readGyro();
      error = setpoint - angle;
      //// Motor Controls
    }
    StopMotors();
  }
  else{
    while (error >= 2){
      PWM_L = error * Kp;
      PWM_R = error * Kp;
      
      ledcWrite(L_IN1, 0);          // L Forward == 0
      ledcWrite(L_IN2, int(PWM_L)); // L Backward 
      ledcWrite(R_IN3, int(PWM_R)); // R Forward
      ledcWrite(R_IN4, 0);          // R Backward == 0
      
      angle += readGyro();
      error = setpoint - angle;
    }
    StopMotors();
  }
}

// Rotate to any desired angle greater than 120 degrees
void RotateToBigAngle(float setpoint){
  float angle = 0;
  float error = setpoint;
  float PWM_L = 0;
  float PWM_R = 0;
  float Kp = 255/setpoint;
  if (error < 0){
    while (error <= -2){
      PWM_L = error * Kp;
      PWM_R = error * Kp;

      if (PWM_L < 60){
        ledcWrite(L_IN1, 60); // L Forward
        ledcWrite(L_IN2, 0);          // L Backward == 0
        ledcWrite(R_IN3, 0);          // R Forward == 0
        ledcWrite(R_IN4, 60); // R Backward
      }

      else{
        ledcWrite(L_IN1, int(PWM_L)); // L Forward
        ledcWrite(L_IN2, 0);          // L Backward == 0
        ledcWrite(R_IN3, 0);          // R Forward == 0
        ledcWrite(R_IN4, int(PWM_R)); // R Backward
      }
      angle += readGyro();
      error = setpoint - angle;
      //// Motor Controls
    }
    StopMotors();
  }
  else{
    while (error >= 2){
      PWM_L = error * Kp;
      PWM_R = error * Kp;
      
      if (PWM_L < 60){
        ledcWrite(L_IN1, 0);          // L Forward == 0
        ledcWrite(L_IN2, 60); // L Backward 
        ledcWrite(R_IN3, 60); // R Forward
        ledcWrite(R_IN4, 0);          // R Backward == 0
      }
      else{
        ledcWrite(L_IN1, 0);          // L Forward == 0
        ledcWrite(L_IN2, int(PWM_L)); // L Backward 
        ledcWrite(R_IN3, int(PWM_R)); // R Forward
        ledcWrite(R_IN4, 0);          // R Backward == 0
      }
      angle += readGyro();
      error = setpoint - angle;
    }
    StopMotors();
  }
}

void RotateSearch(){
  RotateToSmallAngle(20);
  delay(1200);
}

void Forward() {
  int speed = 180; 
  ledcWrite(L_IN1, speed);
  ledcWrite(L_IN2, 0);
  ledcWrite(R_IN3, speed);
  ledcWrite(R_IN4, 0);
}

void StopMotors() {
  ledcWrite(L_IN1, 0);
  ledcWrite(L_IN2, 0);

  ledcWrite(R_IN3, 0);
  ledcWrite(R_IN4, 0);
}

void StraightBack(){
  float maxPWM = 200;
  float setpoint = 0;
  float angle = 0;
  float error = setpoint;
  float PWM_L = 0;
  float PWM_R = 0;
  float Kp = maxPWM/10;

  angle += readGyro();
  error = setpoint + angle;
  for (int temp = 0; temp < 9; temp++){

    if (error > 1){
      PWM_L = maxPWM - abs(error * Kp);
      PWM_R = maxPWM - abs(error * Kp);

      ledcWrite(L_IN1, 0);                   // L Forward == maxPWM - error Correction
      ledcWrite(L_IN2, int(PWM_L));              // L Backward == 0
      ledcWrite(R_IN3, 0);                   // R Forward == maxPWM
      ledcWrite(R_IN4, maxPWM);          // R Backward

      angle += readGyro();
      error = setpoint + angle;
    }
    else if (error < -1){
      PWM_L = maxPWM - abs(error * Kp);
      PWM_R = maxPWM - abs(error * Kp);

      ledcWrite(L_IN1, 0);     // L Forward == maxPWM
      ledcWrite(L_IN2, maxPWM);          // L Backward == 0
      ledcWrite(R_IN3, 0); // R Forward == maxPWM - error Correction
      ledcWrite(R_IN4, int(PWM_R));          // R Backward == 0
      
      angle += readGyro();
      error = setpoint + angle;
    }
    else{
      PWM_L = maxPWM - abs(error * Kp);
      PWM_R = maxPWM - abs(error * Kp);
      
      ledcWrite(L_IN1, 0);                   // L Forward == maxPWM - error Correction
      ledcWrite(L_IN2, int(PWM_L));          // L Backward == 0
      ledcWrite(R_IN3, 0);                   // R Forward == maxPWM
      ledcWrite(R_IN4, int(PWM_R));          // R Backward
      
      angle += readGyro();
      error = setpoint + angle;
    }
    angle += readGyro();
    error = setpoint + angle;
  }
  StopMotors();
}

float updateUltraAverage(float newReading) {
  // Replace the oldest value in the buffer
  ultraDistanceBuffer[ultraIndex] = newReading;
  ultraIndex = (ultraIndex + 1) % ULTRA_SAMPLES;

  // Compute the average
  float sum = 0;
  for (int i = 0; i < ULTRA_SAMPLES; i++) {
    sum += ultraDistanceBuffer[i];
  }
  return sum / ULTRA_SAMPLES;
}
